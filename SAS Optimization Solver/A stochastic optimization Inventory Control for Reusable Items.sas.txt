/*
Authored By: Amir H. Sadeghi
Authored On: 2023-01-15
Authored To: Non-linear Optimization Problem for Solving Reuseable Products

Change Logs: 

Updated By: NA
Updated on: NA
Updated to: NA
*/

proc optmodel;

    **Sets;
    set <num> buyer = {1..4}; * buyer index;
    set <num> product = {1..4}; * product index;

    * ======================================================;

    **Parameters;
    *The inventory level of usable product k of buyer j;
    num ILU{buyer, product} = [ 100 100 100 100
                                100 100 100 100
                                100 100 100 100
                                100 100 100 100];
    *The inventory level of recoverable product k of buyer j;
    num ILR{buyer, product} = [ 100 100 100 100
                                100 100 100 100
                                100 100 100 100
                                100 100 100 100];
    *Fixed ordering cost of supplier per order of reusable product k from buyer j;
    num OCS{buyer, product} = [ 1 1 1 1
                                1 1 1 1
                                1 1 1 1
                                1 1 1 1]; 
    *Fixed ordering cost of reusable product k for buyer j;
    num OCU{buyer, product} = [ 1 1 1 1
                                1 1 1 1
                                1 1 1 1
                                1 1 1 1]; 
    *Fixed recovery cost of recoverable product k for buyer j;
    num OCR{buyer, product} = [ 1 1 1 1
                                1 1 1 1
                                1 1 1 1
                                1 1 1 1]; 
    *Purchasing cost per unit of reusable product k;
    num PC{product} = [10 10 10 10];
    num MU_PC{product} = [10 10 10 10];
    num SIGMA_PC{product} = [1 1 1 1];
    *Recovery cost per unit of recoverable product k for buyer j;
    num RC{buyer, product} = [ 1 1 1 1
                               1 1 1 1
                               1 1 1 1
                               1 1 1 1]; 
    *The holding cost of usable product k per unit for buyer j in each period;
    num HCU{buyer, product} = [ 1 1 1 1
                                1 1 1 1
                                1 1 1 1
                                1 1 1 1]; 
    num MU_HCU{buyer, product} = [1 1 1 1
                                  1 1 1 1
                                  1 1 1 1
                                  1 1 1 1];
    num SIGMA_HCU{buyer, product} = [0.1 0.1 0.1 0.1
                                0.1 0.1 0.1 0.1
                                0.1 0.1 0.1 0.1
                                0.1 0.1 0.1 0.1]; 
    *The holding cost of recoverable product k per unit for buyer j in each period;
    num HCR{buyer, product} = [1 1 1 1
                               1 1 1 1
                               1 1 1 1
                               1 1 1 1];
    num MU_HCR{buyer, product} = [1 1 1 1
                                  1 1 1 1
                                  1 1 1 1
                                  1 1 1 1];
    num SIGMA_HCR{buyer, product} = [0.1 0.1 0.1 0.1
                                0.1 0.1 0.1 0.1
                                0.1 0.1 0.1 0.1
                                0.1 0.1 0.1 0.1]; 
    *The demand rate of reusable product k for buyer j in each period;
    num D{buyer, product} = [ 10 10 10 10
                              10 10 10 10
                              10 10 10 10
                              10 10 10 10];
    num MU_D{buyer, product} = [ 10 10 10 10
                              10 10 10 10
                              10 10 10 10
                              10 10 10 10];
    num SIGMA_D{buyer, product} = [1 1 1 1
                                  1 1 1 1
                                  1 1 1 1
                                  1 1 1 1];
    *The maximum numbers that reusable product k can be reused and recovered;
    num m{product} = [1 1 1 1];
    *The required warehouse space for storing of reusable product k;
    num f{product} = [10 10 10 10];
    num MU_f{product} = [10 10 10 10];
    num SIGMA_f{product} = [1 1 1 1];
    *Total available budget of buyer j;
    num B{product} = [100 100 100 100];
    num MU_B{product} = [100 100 100 100];
    num SIGMA_B{product} = [1 1 1 1];
    *Total allowable holding cost for usable products of buyer j;
    num AHU{buyer} = [1000 1000 1000 1000];
    num MU_AHU{product} = [1000 1000 1000 1000];
    num SIGMA_AHU{product} = [1 1 1 1];
    *Total allowable holding cost for recoverable products j;
    num AHR{buyer} = [1000 1000 1000 1000];
    num MU_AHR{product} = [1000 1000 1000 1000];
    num SIGMA_AHR{product} = [1 1 1 1];
    *Total warehouse space of buyer j for usable products;
    num WSU{buyer} = [1000 1000 1000 1000];
    num MU_WSU{product} = [1000 1000 1000 1000];
    num SIGMA_WSU{product} = [1 1 1 1];
    *Total warehouse space of buyer j for recoverable products;
    num WSR{buyer} = [1000 1000 1000 1000];
    num MU_WSR{product} = [1000 1000 1000 1000];
    num SIGMA_WSR{product} = [1 1 1 1];
    *Total warehouse space of supplier;
    num WS = 1000;
    num MU_WS = 1000;
    num SIGMA_WS = 10;
    *Total allowable number of orders for all products;
    num N = 1000;
    num MU_N = 1000;
    num SIGMA_N = 10;
    *Total inventory cost of buyer j;
    num TCB{buyer} = [1000 1000 1000 1000];
    *Total inventory cost of supplier;
    num TCS = 1000;
    * The probability of violating each of the constraints;
    num z_alpha = 1.96;

    * ======================================================;

    **Decision variables;
    * The economic reuse and recovery quantity for reusable product k of buyer j per cycle;
    var q{buyer, product} >= 0;
    * The ratio of economic order quantity to economic reuse and recovery quantity for reusable product k of buyer j per cycle;
    var p{buyer, product} >= 0;

    * ======================================================;

    /* minimize the total cost of buyer j */
    minimize Cost = sum{j in buyer, k in product}PC[k]*D[j,k]/(m[k]+1) + 
                    sum{j in buyer, k in product}OCU[j,k]*D[j,k]/((m[k]+1)*p[j,k]*q[j,k]) +
                    sum{j in buyer, k in product}OCR[j,k]*D[j,k]*m[k]/(m[k]+1) +
                    sum{j in buyer, k in product}RC[j,k]*D[j,k]*m[k]/((m[k]+1)*q[j,k]) +
                    sum{j in buyer, k in product}HCU[j,k]*p[j,k]*q[j,k]/(2) +
                    sum{j in buyer, k in product}HCR[j,k]*m[k]*q[j,k]/(2*(m[k]+1))+
                    sum{j in buyer, k in product}OCS[j,k]*D[j,k]/((m[k]+1)*p[j,k]*q[j,k]); *sinle supplier;

    * ======================================================;

    ** subject to;
    *Total available budget of buyer j; 
    con BUDGET{j in buyer}: sum{k in product}MU_PC[k]*p[j,k]*q[j,k] + z_alpha*SQRT(sum{k in product}(SIGMA_PC[k]*p[j,k]*q[j,k])^2+SIGMA_B[j]^2) <= MU_B[j];
    *Total warehouse space of usable items (supplier);
    con SUPPLIER_SPACE: sum{j in buyer, k in product}MU_f[k]*p[j,k]*q[j,k] + z_alpha*SQRT(sum{j in buyer, k in product}(SIGMA_f[k]*p[j,k]*q[j,k])^2+SIGMA_WS^2) <= MU_WS;
    *Total warehouse space of usable items (buyer); 
    con BUYER_SPACE_USABLE{j in buyer}: sum{k in product}MU_f[k]*p[j,k]*q[j,k] + z_alpha*SQRT(sum{k in product}(SIGMA_f[k]*p[j,k]*q[j,k])^2+SIGMA_WSU[j]^2) <= MU_WSU[j];
    *Total warehouse space of recoverable items (buyer); 
    con BUYER_SPACE_RECOVERED{j in buyer}: sum{k in product}MU_f[k]*q[j,k] + z_alpha*SQRT(sum{k in product}(SIGMA_f[k]*q[j,k])^2+SIGMA_WSR[j]^2) <= MU_WSR[j];
    *Total holding cost of usable items (buyer); 
    con BUYER_HOLDING_USABLE{j in buyer}: sum{k in product}MU_HCU[j,k]*p[j,k]*q[j,k]/2 + z_alpha*SQRT(sum{k in product}(SIGMA_HCU[j,k]*p[j,k]*q[j,k]/2)^2+SIGMA_AHU[j]^2) <= MU_AHU[j];
    *Total holding cost of recoverable items (buyer); 
    con BUYER_HOLDING_RECOVERED{j in buyer}: sum{k in product}MU_HCR[j,k]*m[k]*q[j,k]/(2*(m[k]+1)) + z_alpha*SQRT(sum{k in product}(SIGMA_HCR[j,k]*m[k]*q[j,k]/(2*(m[k]+1)))^2+SIGMA_AHR[j]^2) <= MU_AHR[j];
    *Total allowable number of orders;
    con ORDERS: sum{j in buyer, k in product}MU_D[j,k]/((m[k]+1)*p[j,k]*q[j,k]) + z_alpha*SQRT(sum{j in buyer, k in product}(SIGMA_D[j,k]/((m[k]+1)*p[j,k]*q[j,k]))^2+SIGMA_N^2) <= MU_N;


    solve with sqp;

    /* print the optimal solution */
    print q;
